plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 19
        multiDexEnabled true
        targetSdkVersion 31
        // Version code for next release (version 9 is now in Play Store)
        versionCode 11
        versionName "1.11"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        setProperty("archivesBaseName", "vision-automl-remote-model-quickstart")
    }
    buildTypes {
        proguard {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', 'proguard.cfg'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro', 'proguard.cfg'
        }
        testBuildType "proguard"
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles 'proguard-rules.pro'
        }
    }

    // Do NOT compress tflite model files (need to call out to developers!)
    aaptOptions {
        noCompress "tflite"
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }
}

repositories {
    maven { url 'https://google.bintray.com/tensorflow' }
    // Depending on AndroidX Snapshot Builds to get the latest CameraX libs.
    maven { url 'https://androidx.dev/snapshots/builds/6787662/artifacts/repository/' }
}

dependencies {
    implementation project (':internal:common')
    // Image labeling with AutoML model support
    implementation 'com.google.mlkit:image-labeling-custom:17.0.0'
    // Or comment the dependency above and uncomment the dependency below to
    // use unbundled model that depends on Google Play Services
    // implementation 'com.google.android.gms:play-services-mlkit-image-labeling-custom:16.0.0-beta2'

    implementation 'com.google.mlkit:object-detection-custom:16.3.4'
    implementation 'com.google.mlkit:linkfirebase:16.1.1'

    implementation 'androidx.multidex:multidex:2.0.1'
    // -------------------------------------------------------

    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'com.google.guava:guava:31.0.1-android'

    // For how to setup gradle dependencies in Android X, see:
    // https://developer.android.com/training/testing/set-up-project#gradle-dependencies
    // Core library
    androidTestImplementation 'androidx.test:core:1.4.0'

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'

    // Assertions
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'

    // ViewModel and LiveData
    implementation "androidx.lifecycle:lifecycle-livedata:2.4.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel:2.4.0"

    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'androidx.annotation:annotation:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'

    // CameraX
    implementation "androidx.camera:camera-camera2:${camerax_version}"
    implementation "androidx.camera:camera-lifecycle:${camerax_version}"
    implementation "androidx.camera:camera-view:1.0.0-alpha24"
}

configurations {
    // Resolves dependency conflict caused by some dependencies use
    // com.google.guava:guava and com.google.guava:listenablefuture together.
    all*.exclude group: 'com.google.guava', module: 'listenablefuture'
}

project.afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
            }
        }
    }
}
